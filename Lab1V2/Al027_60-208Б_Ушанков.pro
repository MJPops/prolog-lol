/*      Какое Мороженое самое вкусное? 
Ирена любит мороженое с фруктами. В кафе был выбор из таких вариантов: 
    - пломбир с орехами; 
    - пломбир с бананами; 
    - пломбир с черникой; 
    - шоколадное с черникой; 
    - шоколадное с клубникой. 
В четырёх вариантах Ирене не нравились или тип мороженого, или наполнитель, а в 
одном  варианте  ей  не  нравились  ни  мороженое,  ни  наполнитель.  Она  попросила 
приготовить из имеющихся продуктов порцию по своему вкусу. Какое же мороженое и с 
какими фруктами любит Ирена?*/

DOMAINS 
    мороженное = пломбир; шоколадное
    наполнитель = орехи; бананы; черника; клубника
    
    элемент_меню = готовое_мороженное(мороженное, наполнитель)
    список_меню = элемент_меню*
    
    гипотеза = любимое_мороженное(мороженное, наполнитель)
PREDICATES
       nondeterm выбор_мороженного(элемент_меню)
       nondeterm возможное_мороженное(мороженное)
       nondeterm возможный_наполнитель(наполнитель)
       
       nondeterm генерация_гипотезы(гипотеза)
       nondeterm проверка_гипотезы(гипотеза)
       nondeterm число_плохих_мороженных_или_наполнителей(гипотеза, список_меню, integer)
       nondeterm число_плохих_мороженных_и_наполнителей(гипотеза, список_меню, integer)
       
       nondeterm не_тип_или_наполнитель(гипотеза, элемент_меню)
       nondeterm не_тип_и_наполнитель(гипотеза, элемент_меню)
CLAUSES 
  выбор_мороженного(готовое_мороженное(пломбир, орехи)).
  выбор_мороженного(готовое_мороженное(пломбир, бананы)).
  выбор_мороженного(готовое_мороженное(пломбир, черника)).
  выбор_мороженного(готовое_мороженное(шоколадное, черника)).
  выбор_мороженного(готовое_мороженное(шоколадное, клубника)).
  
  возможное_мороженное(пломбир).
  возможное_мороженное(шоколадное).
  
  возможный_наполнитель(орехи).
  возможный_наполнитель(бананы).
  возможный_наполнитель(черника).
  возможный_наполнитель(клубника).
  
  генерация_гипотезы(любимое_мороженное(Мороженное, Наполнитель)):-
    возможное_мороженное(Мороженное),
    возможный_наполнитель(Наполнитель).
    
  проверка_гипотезы(Гипотеза):-
    % формируется список из меню
    findall(Мороженное, выбор_мороженного(Мороженное), Меню),
    % подсчет числа элементов меню которые не нравятся частично и полностью
    число_плохих_мороженных_или_наполнителей(Гипотеза, Меню, 4),
    число_плохих_мороженных_и_наполнителей(Гипотеза, Меню, 1).
  
  не_тип_или_наполнитель(
    любимое_мороженное(ЛюбимоеМороженное, ЛюбимыйНаполнитель),
    готовое_мороженное(ГотовоеМороженное, ГотовыйНаполнитель)
  ):-
    ЛюбимоеМороженное = ГотовоеМороженное,
    NOT(ЛюбимыйНаполнитель = ГотовыйНаполнитель);
    ЛюбимыйНаполнитель = ГотовыйНаполнитель,
    NOT(ЛюбимоеМороженное = ГотовоеМороженное).
    
  не_тип_и_наполнитель(
    любимое_мороженное(ЛюбимоеМороженное, ЛюбимыйНаполнитель),
    готовое_мороженное(ГотовоеМороженное, ГотовыйНаполнитель)
  ):-
    NOT(ЛюбимыйНаполнитель = ГотовыйНаполнитель),
    NOT(ЛюбимоеМороженное = ГотовоеМороженное).
  
  число_плохих_мороженных_или_наполнителей(_Любимое, [], 0).
  число_плохих_мороженных_или_наполнителей(Любимое, [Первое|Остальные], Количество):-
    не_тип_или_наполнитель(Любимое, Первое),
    число_плохих_мороженных_или_наполнителей(Любимое, Остальные, КоличествоОстальных),
    Количество = КоличествоОстальных+1;
    %
    NOT(не_тип_или_наполнитель(Любимое, Первое)),
    число_плохих_мороженных_или_наполнителей(Любимое, Остальные, КоличествоОстальных),
    Количество = КоличествоОстальных.
    
  число_плохих_мороженных_и_наполнителей(_Любимое, [], 0).
  число_плохих_мороженных_и_наполнителей(Любимое, [Первое|Остальные], Количество):-
    не_тип_и_наполнитель(Любимое, Первое),
    число_плохих_мороженных_и_наполнителей(Любимое, Остальные, КоличествоОстальных),
    Количество = КоличествоОстальных+1;
    NOT(не_тип_и_наполнитель(Любимое, Первое)),
    число_плохих_мороженных_и_наполнителей(Любимое, Остальные, Количество).
    
GOAL   
/* 
        возможное_мороженное(Мороженное),
        возможный_наполнитель(Наполнитель).
        */
        
        % генерация_гипотезы(Гипотеза).
        
         %findall(Мороженное, выбор_мороженного(Мороженное), Меню).
        
        генерация_гипотезы(Гипотеза),
        проверка_гипотезы(Гипотеза).
        
        %генерация_гипотезы(Х).
  %findall(Мороженное, выбор_мороженного(Мороженное), Меню).