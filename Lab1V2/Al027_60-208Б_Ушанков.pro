/*      Какое Мороженое самое вкусное? 
Ирена любит мороженое с фруктами. В кафе был выбор из таких вариантов: 
    - пломбир с орехами; 
    - пломбир с бананами; 
    - пломбир с черникой; 
    - шоколадное с черникой; 
    - шоколадное с клубникой. 
В четырёх вариантах Ирене не нравились или тип мороженого, или наполнитель, а в 
одном  варианте  ей  не  нравились  ни  мороженое,  ни  наполнитель.  Она  попросила 
приготовить из имеющихся продуктов порцию по своему вкусу. Какое же мороженое и с 
какими фруктами любит Ирена?*/

% Как я понял в задаче опечатка, так как банан, черника и клубника - ягоды

CONSTANTS
% Данные константы используются при установке числа мороженых, которые не нравятся полностью или частично
  количество_мороженого_не_нравящегося_частично = 4
  количество_мороженого_не_нравящегося_полностью = 1
DOMAINS
% Структура объекта (аналогична структуре в задаче)
  мороженое = мороженое(тип_мороженого, наполнитель)
  множество_мороженых = мороженое*

  тип_мороженого = symbol
  наполнитель = symbol

% Гипотеза - то, какое мороженое любит 
  мороженое_ирены = мороженое_ирены(тип_мороженого, наполнитель)
PREDICATES
% Описание измевстных значения из задачи
  nondeterm возможное_тип_мороженого(тип_мороженого)
  nondeterm возможный_наполнитель(наполнитель)
% Формирование списка известных мороженых
  nondeterm доступное_мороженое_в_кафе(мороженое)

% Правила, определяющие критерии, по которым мороженое не подходит
  nondeterm тип_мороженого_не_подходит(мороженое_ирены, мороженое)
  nondeterm наполнитель_не_подходит(мороженое_ирены, мороженое)

% Пересечение п2х верхних правил, составляющие условия задачи
  nondeterm не_подходит_тип_мороженого_ИЛИ_наполнитель(мороженое_ирены, мороженое)
  nondeterm не_подходит_тип_И_наполнитель(мороженое_ирены, мороженое)

% По константам выставляются то, сколько мороженых не нравится частично
  nondeterm установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(мороженое_ирены, множество_мороженых, integer)
% и сколько полностью
  nondeterm установить_число_неподходящих_мороженых_по_типу_И_наполнителей(мороженое_ирены, множество_мороженых, integer)

% Генерация гипотезы
  nondeterm определение_мороженого_ирины(мороженое_ирены)
% Проверка гипотезы
  nondeterm получение_мороженого_ирины(мороженое_ирены)
CLAUSES
  возможное_тип_мороженого(Тип) :-
    Тип = пломбир;
    Тип = шоколадное.
  
  возможный_наполнитель(Наполнитель) :-
    Наполнитель = орехи;
    Наполнитель = бананы;
    Наполнитель = черника;
    Наполнитель = клубника.
  
  доступное_мороженое_в_кафе(Мороженое) :-
    Мороженое = мороженое(пломбир, орехи);
    Мороженое = мороженое(пломбир, бананы);
    Мороженое = мороженое(пломбир, черника);
    Мороженое = мороженое(шоколадное, черника);
    Мороженое = мороженое(шоколадное, клубника).
  
  определение_мороженого_ирины(мороженое_ирены(Мороженое, Наполнитель)) :-
    возможное_тип_мороженого(Мороженое),
    возможный_наполнитель(Наполнитель).

  тип_мороженого_не_подходит(мороженое_ирены(ТипМороженогоИрены, _), мороженое(ТипМороженого, _)) :- 
    not(ТипМороженого = ТипМороженогоИрены).

  наполнитель_не_подходит(мороженое_ирены(_, НаполнительИрены), мороженое(_, Наполнитель)) :-
    not(НаполнительИрены = Наполнитель).
    
% Если правило выполнено, то мороженое не нравится только частично
  не_подходит_тип_мороженого_ИЛИ_наполнитель(МороженоеИрены, Мороженое) :-
  % Либо не подходит тип мороженого
    тип_мороженого_не_подходит(МороженоеИрены, Мороженое),
  % При этом наполнитель подошел
    not(наполнитель_не_подходит(МороженоеИрены, Мороженое));
  % Либо не подходит наполнитель
    наполнитель_не_подходит(МороженоеИрены, Мороженое),
  % Но тогда тип мороженого подошел
    not(тип_мороженого_не_подходит(МороженоеИрены, Мороженое)).
    
% Если правило выполнилось, то мороженое не нравится полностью
  не_подходит_тип_И_наполнитель(МороженоеИрены,Мороженое) :-
  % Не подошел тип мороженого
    тип_мороженого_не_подходит(МороженоеИрены, Мороженое),
  % И наполнитель
    наполнитель_не_подходит(МороженоеИрены, Мороженое).
    
% В пустом списке нет неподходящего мороженого
  установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(_, [], 0).
  установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(МороженоеИрены, [Первое|Остальные], КоличествоНеподходящегоМороженого):-

    установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(МороженоеИрены, Остальные, КоличествоОстальных),
  % Если мороженое не подходит
    не_подходит_тип_мороженого_ИЛИ_наполнитель(МороженоеИрены, Первое),
  % То количество неподходящего мороженого увеличивается
    КоличествоНеподходящегоМороженого = КоличествоОстальных + 1;

    установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(МороженоеИрены, Остальные, КоличествоНеподходящегоМороженого),
  % Если мороженое подходит (not(не подходит) => подходит), то количество не меняется
    not(не_подходит_тип_мороженого_ИЛИ_наполнитель(МороженоеИрены, Первое)).
    
% В пустом списке нет неподходящего мороженого
  установить_число_неподходящих_мороженых_по_типу_И_наполнителей(_, [], 0).
  установить_число_неподходящих_мороженых_по_типу_И_наполнителей(МороженоеИрены, [Первое|Остальные], КоличествоНеподходящегоМороженого):-

    установить_число_неподходящих_мороженых_по_типу_И_наполнителей(МороженоеИрены, Остальные, КоличествоОстальных),
  % Если мороженое не подходит
    не_подходит_тип_И_наполнитель(МороженоеИрены, Первое),
  % То количество не меняется
    КоличествоНеподходящегоМороженого = КоличествоОстальных + 1;

    установить_число_неподходящих_мороженых_по_типу_И_наполнителей(МороженоеИрены, Остальные, КоличествоНеподходящегоМороженого),
  % Если мороженое подходит (not(не подходит) => подходит), то количество не меняется
    not(не_подходит_тип_И_наполнитель(МороженоеИрены, Первое)).
  
  получение_мороженого_ирины(МороженоеИрены):-
  % Устанавливаем вид мороженого Ирины
    определение_мороженого_ирины(МороженоеИрены),
  % Назодим все доступное мороженое
    findall(Мороженое, доступное_мороженое_в_кафе(Мороженое), МножествоМороженых),
  % Устанавливается количество не нравящегося полностью или частично
    установить_число_неподходящих_мороженых_по_типу_ИЛИ_наполнителей(МороженоеИрены, МножествоМороженых, количество_мороженого_не_нравящегося_частично),
    установить_число_неподходящих_мороженых_по_типу_И_наполнителей(МороженоеИрены, МножествоМороженых, количество_мороженого_не_нравящегося_полностью).
GOAL   
  получение_мороженого_ирины(Ирина_любит).